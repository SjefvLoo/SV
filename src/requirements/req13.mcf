% Measurement only takes place when a non-closing wafer is at Meas.
% Depends on aux1.

% C represents the set of positions where the closing wafers are located
(nu X(C: Set(Position) = {ChuckMeas, ChuckProj}).
    % Maintain C
    (forall p:RobotPositionT.
        % Prune the robot x positions options that are not allowed
        val(p in allowed)  => (
            [RMoveFromTo(robot(p),from(p),to(p))](
                val(from(p) in C) => X(C-{from(p)}+{to(p)})
            )
            &&
            [RMoveFromTo(robot(p),from(p),to(p))](
                val(!(from(p) in C)) => X(C)
            )
        )
    )
    &&
    (val(ChuckMeas in C && ChuckProj in C) => [Swap]X(C)) &&
    (val(!(ChuckMeas in C) && !(ChuckProj in C)) => [Swap]X(C)) &&
    (val(!(ChuckMeas in C) && ChuckProj in C) => 
        [Swap]X(C-{ChuckProj}+{ChuckMeas})) &&
    (val(ChuckMeas in C && !(ChuckProj in C)) => 
        [Swap]X(C-{ChuckMeas}+{ChuckProj})) &&
    [(
        % Prune the robot x positions options that are not allowed
        (forall p:RobotPositionT. 
            val(p in allowed)  => !RMoveFromTo(robot(p),from(p),to(p))) &&
        !Swap
    )]X(C) &&
    
    % Use C
    [MeasureWafer]val(!(ChuckMeas in C))
)

&&

% w maps a position to a boolean indicating whether or not a wafers is present.
% Initially every position is false except ChuckMeas and ChuckProj
(nu X(w : Position -> Bool = (lambda p:Position. false)
                                [ChuckMeas -> true]
                                [ChuckProj -> true]).
    % Maintain w
    forall p:RobotPositionT.
        % Prune the robot x positions options that are not allowed
        val(p in allowed) => (
            [RMoveFromTo(robot(p),from(p),to(p))](X(w[from(p) -> false]))
            &&
            [RIdle(robot(p), from(p),to(p))](X(w[to(p) -> true]))
        ) &&
    [(
        forall p:RobotPositionT.
        val(p in allowed) => (
            !RMoveFromTo(robot(p), from(p), to(p)) 
            &&
            !RIdle(robot(p), from(p), to(p))
        )
    )]X(w) &&

    % Use w
    [MeasureWafer]val(w(ChuckMeas))
)