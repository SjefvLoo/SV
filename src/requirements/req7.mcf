% Wafers that are being projected have been measured at Meas.
% Depends on aux1.

% mm indicates whether the wafer at position ChuckMeas has been measured.
% pm indicates whether the wafer at position ChuckProj has been measured.
nu X(mm: Bool = false, pm: Bool = false).
    % Maintain mm and pm
    [Measured]X(true, pm) &&
    [SwapIdle]X(pm, mm) &&
    (forall p:RobotPositionT.
        % Prune the robot x positions options that are not allowed
        val(p in allowed)  => (
            [RMoveFromTo(robot(p),from(p),to(p))](
                val(from(p) == ChuckMeas) => X(false,pm)
            )
            &&
            [RMoveFromTo(robot(p),from(p),to(p))](
                val(!(from(p) == ChuckMeas)) => X(mm,pm)
            )
        )
    ) &&
    [(
        % Prune the robot x positions options that are not allowed
        (forall p:RobotPositionT. 
            val(p in allowed)  => !RMoveFromTo(robot(p),from(p),to(p))) &&
        !Measured &&
        !SwapIdle &&
        !ProjectWafer
    )]X(mm,pm) &&
    
    % Use mm and pm
    [ProjectWafer](val(pm == true) && X(mm,pm))
